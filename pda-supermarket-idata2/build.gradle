apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("MMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    signingConfigs {
        debug {
        }
        release {
        }
    }

    defaultConfig {
        applicationId "com.manfenjiayuan.pda_supermarket"

        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 17
        versionName "0.1.0"
        multiDexEnabled true

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

        manifestPlaceholders = [
                UMENG_APP_KEY   : "5733f880e0f55a5eb6002e54", // 友盟 app key
                GETUI_APP_ID    : "6SPe74UAPm9Ti2rImq1uRA", // 个推 app id
                GETUI_APP_KEY   : "xjfqAF81Nl9uLf0fffu8m1", // 个推 app key
                GETUI_APP_SECRET: "TEKMiptTNf8rI9E8Myz6p3", // 个推 app secret
                PACKAGE_NAME    : applicationId // apk 包名
        ]
    }
    buildTypes {
        debug {
//            buildConfigField "boolean", "LOG_DEBUG", "true"//是否输出LOG信息
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
//            buildConfigField "boolean", "LOG_DEBUG", "false"//是否输出LOG信息
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName
                if (variant.buildType.name == "release") {
                    // 输出apk名称为mfh_cashier_v1.0.1_101915_wandoujia.apk
                    fileName = "mfh_pda_supermarket_${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${releaseTime()}${defaultConfig.versionCode}.apk"
                } else {
                    fileName = "mfh_pda_supermarket_${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${releaseTime()}_beta.apk"
                }

                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
    productFlavors {
        bingshanguxue {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "bingshanguxue"]
        }
        manfenjiayuan {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "manfenjiayuan"]
        }
        mixicook {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "mixicook"]
        }
    }
    File propFile = file('signing.properties');
    if (propFile.exists()) {
        def Properties props = new Properties()
        props.load(new FileInputStream(propFile))
        if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            android.buildTypes.release.signingConfig = null
        }
    } else {
        android.buildTypes.release.signingConfig = null
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
//    compile project(':mfh-framework-all')
//    compile project(':mfh-framework-android')
//    compile project(':mfh-framework-business')
//    compile project(path: ':vector_user')
    compile project(':im')
    compile project(':pda')
}
