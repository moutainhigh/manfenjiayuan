/**
 * The first line in the build configuration applies the Android plugin for
 * Gradle to this build and makes the android {} block available to specify
 * Android-specific build options.
 */
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'


def releaseTime() {
    return new Date().format("MMdd", TimeZone.getTimeZone("UTC"))
}

/**
 * The android {} block is where you configure all your Android-specific
 * build options.
 */
android {

    useLibrary 'org.apache.http.legacy'

    /**
     * compileSdkVersion specifies the Android API level Gradle should use to
     * compile your app. This means your app can use the API features included in
     * this API level and lower.
     *
     * buildToolsVersion specifies the version of the SDK build tools, command-line
     * utilities, and compiler that Gradle should use to build your app. You need to
     * download the build tools using the SDK Manager.
     */
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    signingConfigs {
        debug {
        }
        release {
        }
    }

    /**
     * The defaultConfig {} block encapsulates default settings and entries for all
     * build variants, and can override some attributes in main/AndroidManifest.xml
     * dynamically from the build system. You can configure product flavors to override
     * these values for different versions of your app.
     */
    defaultConfig {
        /**
         * applicationId uniquely identifies the package for publishing.
         * However, your source code should still reference the package name
         * defined by the package attribute in the main/AndroidManifest.xml file.
         */
        applicationId "com.mfh.litecashier"

        // Defines the minimum API level required to run the app.
        minSdkVersion rootProject.ext.android.minSdkVersion

        // Specifies the API level used to test the app.
        targetSdkVersion rootProject.ext.android.targetSdkVersion

        // Defines the version number of your app.
        versionCode 346

        // Defines a user-friendly version name for your app.
        versionName "1.23.1"

        multiDexEnabled true

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi' , 'armeabi-v7a'
        }

        manifestPlaceholders = [
                UMENG_APP_KEY   : "56652e4467e58e75f5001144", // 友盟 app key
                GETUI_APP_ID    : "cEhivbdobh8o4ai7HEQCo9", // 个推 app id
                GETUI_APP_KEY   : "gIjGa6xaFu9t6r380q5RO6", // 个推 app key
                GETUI_APP_SECRET: "5FEagpOP0D7CU4qFP4ulP2", // 个推 app secret
                PACKAGE_NAME    : applicationId // apk 包名
        ]
    }

    /**
     * The buildTypes {} block is where you can configure multiple build types.
     * By default, the build system defines two build types: debug and release. The
     * debug build type is not explicitly shown in the default build configuration,
     * but it includes debugging tools and is signed with the debug key. The release
     * build type applies Proguard settings and is not signed by default.
     */
    buildTypes {
        debug {
//            buildConfigField "boolean", "LOG_DEBUG", "true"//是否输出LOG信息
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        /**
         * By default, Android Studio configures the release build type to enable code
         * shrinking, using minifyEnabled, and specifies the Proguard settings file.
         */
        release {
//            buildConfigField "boolean", "LOG_DEBUG", "false"//是否输出LOG信息
            // Enables code shrinking for the release build type.
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName
                if (variant.buildType.name == "release") {
                    // 输出apk名称为mfh_cashier_v1.0.1_101915_wandoujia.apk
                    fileName = "mfh_litecashier" +
                            "_v${defaultConfig.versionName}_${releaseTime()}${defaultConfig.versionCode}" +
                            "_${variant.productFlavors[0].name}.apk"
                } else {
                    fileName = "mfh_litecashier" +
                            "_v${defaultConfig.versionName}_${releaseTime()}${defaultConfig.versionCode}_beta" +
                            "_${variant.productFlavors[0].name}.apk"
                }

                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    /**
     * The productFlavors {} block is where you can configure multiple product
     * flavors. This allows you to create different versions of your app that can
     * override defaultConfig {} with their own settings. Product flavors are
     * optional, and the build system does not create them by default. This example
     * creates a free and paid product flavor. Each product flavor then specifies
     * its own application ID, so that they can exist on the Google Play Store, or
     * an Android device, simultaneously.
     */
    productFlavors {
        bingshanguxue {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "bingshanguxue"]
        }
        manfenjiayuan {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "manfenjiayuan"]
        }
        mixicook {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "mixicook"]
        }
        poslab {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "poslab"]
        }
    }

    /**
     * 签名配置文件
     * */
    File propFile = file('signing.properties');
    if (propFile.exists()) {
        def Properties props = new Properties()
        props.load(new FileInputStream(propFile))
        if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            android.buildTypes.release.signingConfig = null
        }
    } else {
        android.buildTypes.release.signingConfig = null
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

/**
 * The dependencies {} block in the module-level build configuration file
 * only specifies dependencies required to build the module itself.
 */
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile rootProject.ext.dependencies["cardview-v7"]
    debugCompile rootProject.ext.dependencies["leakcanary"]
    releaseCompile rootProject.ext.dependencies["leakcanary-release"]
    //    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
//    compile project(':im')
    compile project(':cashier')
    compile rootProject.ext.dependencies["crashreport_upgrade"]
    compile rootProject.ext.dependencies["nativecrashreport"]
//    compile 'com.tencent.bugly:nativecrashreport:1.2.1'
    compile rootProject.ext.dependencies["butterknife"]
    apt rootProject.ext.dependencies["butterknife-compiler"]
    compile rootProject.ext.dependencies["getui"]
//    compile 'me.drakeet.multitype:multitype:2.0.0'

}
