import java.text.SimpleDateFormat

apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 8
        versionName "1.0.4"

        multiDexEnabled true

        buildConfigField "String", "SDK_BUILD_TIME", "\"" +  getCurrentTime() + "\""
        buildConfigField "String", "SDK_BUILD_HOST", "\"" +  getUserName() + "\""
        buildConfigField "String", "SDK_BUILD_OS", "\"" +  getOSName() + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/maven/org.slf4j/slf4j-android/pom.properties'
        exclude 'META-INF/maven/org.slf4j/slf4j-android/pom.xml'
//        exclude 'META-INF/LICENSE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile rootProject.ext.dependencies["design"]
    compile rootProject.ext.dependencies["appcompat-v7"]
    compile rootProject.ext.dependencies["recyclerview-v7"]
    compile rootProject.ext.dependencies["butterknife"]
    apt rootProject.ext.dependencies["butterknife-compiler"]
    compile rootProject.ext.dependencies["eventbus"]
    compile rootProject.ext.dependencies["glide"]
    compile project(':mfh-framework-all')
    compile 'me.drakeet.multitype:multitype:2.0.0'

    compile rootProject.ext.dependencies["retrofit2GsonConverter"]
    compile rootProject.ext.dependencies["retrofit2RxJavaAdapter"]
    compile rootProject.ext.dependencies["retrofit2"]
    compile rootProject.ext.dependencies["rxjava"]
    compile rootProject.ext.dependencies["rxandroid"]
    compile rootProject.ext.dependencies["okhttpLoggingInterceptor"]
}


def getCurrentTime() {
    SimpleDateFormat sdf = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss", Locale.US)
    sdf.format(new Date(java.lang.System.currentTimeMillis()))
}

def getUserName() {
    System.getProperty("user.name");
}

def getOSName() {
    System.getProperty("os.name");
}
